apply plugin: 'io.token.gradle.publish'
apply plugin: 'io.token.gradle.lib'
apply plugin: 'com.github.hierynomus.license'

sourceCompatibility = 1.8
targetCompatibility = 1.8

publish {
    isPublic = true
}

dependencies {
    ext.compileExcludeGuava = { dependency ->
        compile (dependency) {
            exclude group: 'com.google.guava', module: 'guava'
        }
    }

    compileExcludeGuava("io.token.proto:tokenio-proto-external:${ver.tokenProto}")
    compileExcludeGuava("io.token.rpc:tokenio-rpc-client-api:${ver.tokenRpc}")
    compileExcludeGuava("io.token.rpc:tokenio-rpc-client-lite:${ver.tokenRpc}")
    compileExcludeGuava("io.token.rpc:tokenio-rpc-client-lite-netty:${ver.tokenRpc}")
    compileExcludeGuava("io.token.security:tokenio-security-lib:${ver.tokenSecurity}")
    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: ver.rxjava
}

license {
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Token, Inc.'
    ext.author = 'Token, Inc.'
    ext.email = 'info@token.io'

    include "**/*.java"
    exclude "**/*Test.java"
    exclude "**/gradle/**/*.java"
    exclude "**/testing/**/*.java"
    exclude "**/asserts/**/*.java"

    skipExistingHeaders = true
}

downloadLicenses {
    includeProjectDependencies = true
    dependencyConfiguration = 'compile'
}

configurations.compileClasspath {
    resolutionStrategy {
        failOnVersionConflict()
    }
}
